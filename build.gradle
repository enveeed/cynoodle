/*
 * Copyright (c) enveeed 2019 - All Rights Reserved.
 * Unauthorized copying of this file, via any medium is strictly prohibited.
 * Proprietary and confidential.
 */

plugins {
    id 'java'
    id 'application'

    id 'idea'
    
    id 'de.fuerstenau.buildconfig' version '1.1.8' 
}

// ======

// gets the git hash of the last commit to use it in the version number
final def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        println project.projectDir
        commandLine 'git', '-C', project.projectDir, 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

final def gitHash = getGitHash.call()

// ======

version "2019.0.0-PR-$gitHash"
group "cynoodle"

// ======

buildConfig {
    appName = project.name
    version = project.version
    packageName = "cynoodle.core"
}

// ======

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' } // JitPack.io
}

// ======

// TODO cynoodle modules definition

// ======

application {
    applicationName = "cynoodle-core"
    mainClassName = "cynoodle.core.Launcher"
}

// ======

test {
    useJUnitPlatform()
}

// ======

dependencies {

    // APIs

    implementation group: 'net.dv8tion', name:'JDA', version: '3.8.3_460' // JDA (Discord API)
    implementation group: 'org.mongodb', name: 'mongo-java-driver', version: '3.10.1' // MongoDB Java Driver (MongoDB API)

    // Utilities / Libraries

    implementation group: 'com.google.guava', name: 'guava', version: '27.0.1-jre' // Google Guava (Common Java Utils)
    implementation group: 'com.google.flogger', name: 'flogger', version: '0.3.1' // Google Flogger API (Fluent Logging)
    implementation group: 'com.google.flogger', name: 'flogger-system-backend', version: '0.3.1' // TODO Google Flogger System Backend (Fluent Logging)
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5' // Google GSON (JSON library)

    implementation 'org.eclipse.collections:eclipse-collections:9.2.0'
    implementation 'org.eclipse.collections:eclipse-collections-api:9.2.0'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.8' // Jackson JSON API

    implementation group: 'com.github.jillesvangurp', name: 'jsonj', version: 'v2.56' // JsonJ fluent JSON API
    
    // base:fm

    implementation group: 'de.u-mass', name: 'lastfm-java', version: '0.1.2' // Last FM API wrapper (TODO replace with own wrapper)
    
    // Unit Testing

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2' // JUnit API
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.2' // JUnit Engine

}